PROCEDURE editBookmark
  (* 9/12/22
  TYPE bookmarks=server,page,title:STRING[144]
  DIM instr:STRING[144]
  DIM spacr,bookfile:STRING[40]
  DIM path:BYTE
  DIM int1,fn2,doloc,dopag,readloc,reclen,recnum:INTEGER
  DIM debug,PgFlg:BOOLEAN
  DIM bookmark,bookmarki:bookmarks
  reclen:=432 \recnum:=1 \spacr:="                                        "
  doloc:=1 \dopag:=0
  ON ERROR GOTO 1000 

  RUN gfx2("dwend") \RUN gfx2("dwset",7,0,0,80,25,0,3,0)
  RUN gfx2("palette",0,0) \RUN gfx2("palette",1,7)
  RUN gfx2("palette",2,56) \RUN gfx2("palette",3,63)
  RUN gfx2("owset",1,5,5,70,14,1,2)
  RUN gfx2("curon") \RUN gfx2("gcset",0,0)

  INPUT "File to create or edit? (bookmark) ",bookfile
  IF bookfile="" THEN 
    bookfile:="bookmark.www"
  ENDIF 
  IF RIGHT$(bookfile,4)<>".www" THEN 
    bookfile:=bookfile+".www"
  ENDIF 

10 OPEN #path,"/dd/SYS/"+bookfile:UPDATE
  SEEK #path, 0
  GET #path,bookmark
  reclen:=VAL(bookmark.page)
  recnum:=VAL(bookmark.title)
  PgFlg:=TRUE
  LOOP 
    IF PgFlg=TRUE THEN 
      PgFlg:=FALSE
      RUN gfx2("clear")
      FOR fn2=0 TO 9
        readloc:=fn2+dopag*10
        IF readloc<>0 THEN
          SEEK #path,readloc*reclen
          GET #path,bookmark
          PRINT  " ";bookmark.title;" - ";bookmark.server; "/"; bookmark.page
        ENDIF
      NEXT fn2
      RUN gfx2("curxy",0,doloc-1) \ RUN gfx2("revon")
      PRINT dopag*10+doloc; \ RUN gfx2("revoff")
    ENDIF 
    RUN gfx2("curxy",0,11) \ RUN gfx2("revon")
    PRINT LEFT$(spacr,18); "Keys: <D>elete <A>dd <E>dit <Q>uit"; LEFT$(spacr,18); 
    PRINT LEFT$(spacr,6); "Arrows: up, down move one line.  left, right move one page"; LEFT$(spacr,5); 
    RUN gfx2("revoff")
    instr:=""
    WHILE instr="" DO 
      RUN bnkey(instr)
    ENDWHILE 
    RUN gfx2("curxy",0,doloc-1) \ RUN gfx2("revoff")
    PRINT " ";  \ RUN gfx2("revon")
    IF instr=CHR$(12) THEN  \(* line up 
      PgFlg:=TRUE
      doloc:=doloc-1
      IF doloc<0 THEN 
        doloc:=9 \dopag:=dopag-1 \PgFlg:=TRUE
      ENDIF 
    ELSE 
      IF instr=CHR$(10) THEN  \(* line down 
        doloc:=doloc+1
        IF doloc>10 THEN 
          doloc:=1 \ dopag:=dopag+1 \ PgFlg:=TRUE
        ENDIF 
      ENDIF 
    ENDIF 
    IF instr=CHR$(9) THEN  \(* page up  
      dopag:=dopag+1 \ PgFlg:=TRUE
    ELSE 
      IF instr=CHR$(8) THEN  \(* page down
        dopag:=dopag-1 \ PgFlg:=TRUE
      ENDIF 
    ENDIF 
    IF dopag<0 THEN 
      dopag:=4
    ENDIF 
    dopag:=MOD(dopag,5)
    readloc:=dopag*10+doloc-1
    RUN gfx2("curxy",0,doloc-1) \ RUN gfx2("revon")
    PRINT " ";  \ RUN gfx2("revoff")

    IF instr="e" OR instr="E" THEN  \(* edit
      SEEK #path,doloc*reclen
      GET #path,bookmark
      PgFlg:=TRUE \ RUN gfx2("clear")
      PRINT "  Title: "; bookmark.title
      INPUT "New text or <Enter> to keep: ",instr
      IF instr<>"" THEN 
        bookmark.title:=instr
      ENDIF 
      PRINT "  Server: "; bookmark.server
      INPUT "New text or <Enter> to keep: ",instr
      IF instr<>"" THEN 
        IF LEFT$(instr,4)="http" THEN 
          instr:=MID$(instr,SUBSTR("//",instr)+2,144)
        ENDIF 
        bookmark.server:=instr
      ENDIF 
      PRINT "  Page: "; bookmark.page
      INPUT "New text or <Enter> to keep: ",instr
      IF instr<>"" THEN 
        bookmark.page:=instr
      ENDIF 
      SEEK #path,readloc*reclen
      PUT #path,bookmark
    ELSE 
    IF instr="d" OR instr="D" THEN  \(* delete waterfall
      PgFlg:=TRUE
      IF doloc<=recnum THEN
        FOR readloc=doloc+1 TO recnum 
          SEEK #path,readloc*reclen \ GET #path,bookmark
          SEEK #path,(readloc-1)*reclen \ PUT #path,bookmark
        NEXT readloc
        bookmark.server=""
        bookmark.page=""
        bookmark.title=""
        SEEK #path,recnum*reclen \ PUT #path,bookmark
        recnum:=recnum-1
        bookmark.server="reclen recnum"
        bookmark.page=STR$(reclen)
        bookmark.title=STR$(recnum)
        SEEK #path,0 \ PUT #path,bookmark
      ENDIF
    ELSE 
      IF instr="a" OR instr="A" THEN  \(* add (insert)
        PgFlg:=TRUE \ RUN gfx2("clear")
        INPUT "Enter a title for the new bookmark: ",instr
        bookmark.title:=instr
        INPUT "Enter the URL for new bookmark: ",instr
        IF LEFT$(instr,4)="http" THEN 
          instr:=MID$(instr,SUBSTR("//",instr)+2,144)
        ENDIF 
        int1:=SUBSTR("/",instr)
        IF int1<>0 THEN 
          bookmark.server:=LEFT$(instr,int1-1)
          (* bookmark.page:=RIGHT$(instr,int1+1)
          bookmark.page:=MID$(instr,int1+1,LEN(instr)-int1)
        ELSE 
          bookmark.server:=instr
          bookmark.page:="index.html"
        ENDIF
        (* open a gap for insert  
        FOR readloc=recnum TO doloc STEP -1 
          SEEK #path,readloc*reclen \ GET #path,bookmarki
          SEEK #path,(readloc+1)*reclen \ PUT #path,bookmarki
        NEXT readloc
        SEEK #path,doloc*reclen \ PUT #path,bookmark
        PgFlg:=TRUE \ recnum:=recnum+1
        bookmark.server="reclen recnum"
        bookmark.page=STR$(reclen)
        bookmark.title=STR$(recnum)
        SEEK #path,0 \ PUT #path,bookmark
      ELSE 
        IF instr="q" OR instr="Q" THEN 
          CLOSE #path \ RUN gfx2("curoff") \ RUN gfx2("owend")
          END 
        ENDIF 
      ENDIF 
    ENDIF 
  ENDIF 
ENDLOOP 

1000 int1:=ERR 
  IF int1=216 THEN
    CREATE #path,"/dd/SYS/"+bookfile:WRITE
    bookmark.server="reclen recnum"
    bookmark.page=STR$(reclen)
    bookmark.title=STR$(recnum)
    PUT #path,bookmark
    CLOSE #path
    GOTO 10
  ENDIF
  PRINT "error: ";int1
  STOP
