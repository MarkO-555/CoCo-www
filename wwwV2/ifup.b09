PROCEDURE ifup
      (* ifup 1.2 - 3/13/24 establish network connections 
      (* uses Axiom's f.config or f.dhcp
      (* use: ifup (   ,ethX, -a)
      (* config: /dd/sys/interfaces, /dd/SYS/hosts 
      
      DIM debug,dhcp,inwork:BOOLEAN
      DIM int1,fn1,path,path2,path3,inum:INTEGER
      DIM iname,ipaddr,gate,net,mac,phyaddr,shost:STRING
      DIM instrg,fhost:STRING[80]
      DIM instr,dns:STRING[160]
      PARAM intface:STRING
      
      debug:=FALSE \inwork:=FALSE \inum:=0
      
      OPEN #path,"/DD/SYS/interfaces":READ
      WHILE EOF(#path)<>TRUE DO 
        GOSUB 200
 10     IF LEFT$(instr,6)="iface " THEN 
          IF inwork=FALSE THEN 
            GOSUB 100 \(* get shorthost
            inwork:=TRUE
          ELSE 
            GOSUB 1000 \(* doConfig 
            GOSUB 100
          ENDIF 
          GOSUB 200
        ENDIF 
        IF inwork=TRUE THEN 
          IF LEFT$(instr,8)="address " THEN 
            ipaddr:=RIGHT$(instr,LEN(instr)-8)
          ENDIF 
          IF LEFT$(instr,8)="gateway " THEN 
            gate:=RIGHT$(instr,LEN(instr)-8)
          ENDIF 
          IF LEFT$(instr,8)="netmask " THEN 
            net:=RIGHT$(instr,LEN(instr)-8)
          ENDIF 
          IF LEFT$(instr,8)="macaddr " THEN 
            mac:=RIGHT$(instr,LEN(instr)-8)
          ENDIF 
          IF LEFT$(instr,8)="phyaddr " THEN 
            IF SUBSTR("FF78",instr)>0 THEN 
              phyaddr:="-wFF78"
            ELSE 
              phyaddr:="-wFF68"
            ENDIF 
          ENDIF 
        ENDIF 
      ENDWHILE 
      CLOSE #path
      GOSUB 1000
      END 
      
 100  (* Get/set interface name and short hostname
      iname:=MID$(instr,7,4) \inum:=inum+1
      IF SUBSTR("dhcp",instr)>0 THEN 
        dhcp:=TRUE
      ELSE 
        dhcp:=FALSE
      ENDIF 
      shost:="coco" \(***FIXME*** get default from ROM RSN
      OPEN #path3,"/DD/SYS/hosts":READ
      WHILE EOF(#path3)<>TRUE DO 
        READ #path3,instrg
        IF LEFT$(instrg,8)="127.0.0." THEN 
          IF VAL(MID$(instrg,9,1))=inum THEN 
            instrg:=RIGHT$(instrg,LEN(instrg)-10)
            int1:=SUBSTR(" ",instrg)
            IF int1>0 THEN 
              fhost:=LEFT$(instrg,int1)
            ELSE 
              fhost:=instrg
            ENDIF 
            shost:=LEFT$(fhost,4)
          ENDIF 
        ENDIF 
      ENDWHILE 
      CLOSE #path3
      iname:=MID$(instr,7,4) \inwork:=TRUE \inum:=inum+1
      IF SUBSTR("dhcp",instr)>0 THEN 
        dhcp:=TRUE
      ELSE 
        dhcp:=FALSE
      ENDIF 
      RETURN 
      
 200  (* readln, strip leading shitespace
      READ #path,instr \instr:=instr+"."
      WHILE LEFT$(instr,1)=" " DO 
        instr:=RIGHT$(instr,LEN(instr)-1)
      ENDWHILE 
      RETURN 
      
 1000 (* configure interface
      (* second interface on hold, set addr crashes coco!
      IF phyaddr="-wFF78" THEN 
        PRINT "DEBUG: iface "; iname; " at phyaddr FF78 disabled (crashes CoCo)"
        PRINT "Any key to skip this interface"
        INPUT a$
        RETURN 
      ENDIF 
      IF dhcp=FALSE THEN 
        IF debug=TRUE THEN 
          PRINT "DEBUG: f.config "; phyaddr; " "; ipaddr; " "; net; " "; gate
        ELSE 
          (* SHELL "f.config "+phyaddr+" "+ipaddr+" "+net+" "+gate
          SHELL "f.config "+ipaddr+" "+net+" "+gate
        ENDIF 
      ELSE 
        IF debug=TRUE THEN 
          PRINT "DEBUG: f.dhcp "; phyaddr; " "; shost; 
          GOSUB 1005
        ENDIF 
      ENDIF 
      ON ERROR GOTO 1001
      SHELL "del /dd/SYS/dhcp.tmp"
 1001 ON ERROR 
      (* SHELL "f.dhcp "+phyaddr+" "+shost+" > /dd/SYS/dhcp.tmp" ***FIXME***
      SHELL "f.dhcp "+shost+" > /dd/SYS/dhcp.tmp"
      dns:="" \instr:=""
      OPEN #path,"/dd/SYS/dhcp.tmp":UPDATE
      READ #path,instr
      WRITE #path,CHR$(13)
      CLOSE #path
      OPEN #path,"/DD/SYS/dhcp.tmp":READ
      WHILE EOF(#path)<>TRUE DO 
        READ #path,instr
        int1:=SUBSTR("dns=",instr)
        IF int1>0 THEN 
          dns:=TRIM$(RIGHT$(instr,LEN(instr)-int1-3))
        ENDIF 
      ENDWHILE 
      CLOSE #path
      (* rewrite resolv.conf, replace or delete old dhcp entry 
      ON ERROR GOTO 1002
      SHELL "del /dd/SYS/resolv.tmp"
 1002 ON ERROR 
      SHELL "rename /dd/SYS/resolv.conf resolv.tmp"
      OPEN #path,"/dd/SYS/resolv.tmp":READ
      CREATE #path2,"/dd/SYS/resolv.conf":WRITE
      WHILE EOF(#path)<>TRUE DO 
        READ #path,instr
        IF SUBSTR("; temporary",instr)=0 THEN 
          WRITE #path2,instr
        ELSE 
          READ #path,instr
          IF dhcp=TRUE THEN  \(* write the new assignment
            IF SUBSTR("nameserver",instr)<>0 THEN 
              IF LEN(dns)>1 THEN 
                IF debug=TRUE THEN 
                  PRINT "writing dhcp nameserver: "; dns
                  GOSUB 1005
                ENDIF 
                dns:=LEFT$(dns,LEN(dns)-1)
                WRITE #path2,"; temporary, from dhcp"
                WRITE #path2,"nameserver "+dns
              ENDIF 
            ENDIF 
          ENDIF  \(* dropping old nameserver assignment on floor
        ENDIF 
      ENDWHILE 
      CLOSE #path \ CLOSE #path2
      
      IF debug=TRUE THEN 
        PRINT "DEBUG: starting "; 
        PRINT "iface: "; iname; " at "; phyaddr
        PRINT "   IP addr",ipaddr
        PRINT "   netmask",net
        PRINT "   gateway",gate
        PRINT "   macaddr",mac
        PRINT "hostname: "; fhost; " "; shost
        GOSUB 1005
      ENDIF 
      RETURN 

 1005 PRINT  \ INPUT "Any key to continue.",a$
      RETURN 
