PROCEDURE ifup
     (* ifup 1.0 - 5/1/23 
     (* establish a network connection using Frobio's f.config or f.dhcp
     (* use: ifup eth0 ***FIXME add name param ethx***
     (* ***FIXME*** check if already up
     (* configure: using 2 linux style conf files:
     (* 
     (* /dd/sys/interfaces formatted in 1 of 2 styles
     (* (Or, change the word 'static' to 'dhcp' to park a config)
     (* 
     (* iface eth0 inet dhcp
     (* phyaddr $FF6B
     (* 
     (* iface eth0 inet static
     (* address 192.168.0.7
     (* gateway 192.168.0.1
     (* netmask 255.255.255.0
     (* macaddr 5C:26:0A:01:02:03
     (* phyaddr $FF6B
     (* 
     (* /dd/SYS/hosts is mainly a dns cache, except first line
     (* should be '127.0.0.1' plus hostname of CoCo.
     (* The 1st 4 chars of the hostname will be used in the dhcp mac
     (* 127.0.0.1   rickscoco.computerconect.com
     (* ...

     
     DIM debug,dhcp:BOOLEAN
     DIM fna,loc,int1,path:INTEGER
     DIM addrs,gate,net,mac,instr,outstr:STRING[80]
     DIM phyaddr:STRING[5] \(* should be a PARAM soon
     debug:=TRUE
     
     OPEN #path,"/DD/SYS/interfaces":READ
     WHILE EOF(#path)<>TRUE DO 
       READ #path,instr \instr:=instr+"."
       IF LEFT$(instr,6)="iface " THEN 
         IF SUBSTR("dhcp",instr)>0 THEN 500
       ENDIF 
       IF LEFT$(instr,8)="address " THEN 
         addrs:=RIGHT$(instr,LEN(instr)-8)
       ENDIF 
       IF LEFT$(instr,8)="gateway " THEN 
         gate:=RIGHT$(instr,LEN(instr)-8)
       ENDIF 
       IF LEFT$(instr,8)="netmask " THEN 
         net:=RIGHT$(instr,LEN(instr)-8)
       ENDIF 
       IF LEFT$(instr,8)="macaddr " THEN 
         mac:=RIGHT$(instr,LEN(instr)-8)
       ENDIF 
       IF LEFT$(instr,8)="phyaddr " THEN 
         IF SUBSTR("FF78",instr)>0 THEN 
           phyaddr:="-wFF78"
         ELSE 
           phyaddr:="-wFF68"
         ENDIF 
       ENDIF 
     ENDWHILE 
     CLOSE #path
     IF debug=TRUE THEN 
       PRINT "From interfaces: "
       PRINT "address",addrs
       PRINT "netmask",net
       PRINT "gateway",gate
       PRINT "macaddr",mac; " (not used)"
     ENDIF 
     SHELL "f.config "+addrs+" "+net+" "+gate
     END 
500  (* dhcp, using hostname from /dd/sys/hosts, else 'coco'
     outstr:="coco"
     OPEN #path,"/DD/SYS/hosts":READ
     WHILE EOF(#path)<>TRUE DO 
       READ #path,instr
       IF LEFT$(instr,10)="127.0.0.1 " THEN 
         instr:=RIGHT$(instr,LEN(instr)-10)
         int1:=SUBSTR(" ",instr)
         IF int1>0 THEN 
           instr:=RIGHT$(instr,LEN(instr)-int1)
           outstr:=LEFT$(instr,MOD(int1,4))
         ENDIF 
       ENDIF 
     ENDWHILE 
     CLOSE #path
     IF debug=TRUE THEN 
       PRINT "Calling f.dhcp with hostname: "; outstr
     ENDIF 
     SHELL "f.dhcp "+outstr
     END 
